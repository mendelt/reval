use rust_decimal::Decimal; 
use std::str::FromStr;
use crate::{expr::Expr, value::Value};

grammar;

match {
    "==" => OP_EQ,
    "!=" => OP_NEQ,
     ">" => OP_GT,
     "<" => OP_LT,
     ">=" => OP_GTE,
     "<=" => OP_LTE,
     "+" => OP_ADD,
     "-" => OP_SUB,
     "*" => OP_MULT,
     "/" => OP_DIV,
     "(" => LBR,
     ")" => RBR,
     r"i[+-]?[0-9]+" => INT,
     r"f[+-]?[0-9]*\.?[0-9]+([eE][-+]?[0-9]+)?" => FLOAT,
     r"d[+-]?[0-9]*\.?[0-9]+" => DECIMAL,
     "true" => TRUE,
     "false" => FALSE,
     "none" => NONE,
}
else {
    r"[a-zA-Z][_a-zA-Z0-9]*" => IDENT,
}

pub Expr: Expr = EqExpr;

EqExpr: Expr = {
    <l:EqExpr> OP_EQ <r:AddExpr> => Expr::eq(l, r),
    <l:EqExpr> OP_NEQ <r:AddExpr> => Expr::neq(l, r),
    <l:EqExpr> OP_GT <r:AddExpr> => Expr::gt(l, r),
    <l:EqExpr> OP_LT <r:AddExpr> => Expr::lt(l, r),
    <l:EqExpr> OP_GTE <r:AddExpr> => Expr::gte(l, r),
    <l:EqExpr> OP_LTE <r:AddExpr> => Expr::lte(l, r),
    AddExpr
}

AddExpr: Expr = {
    <l:AddExpr> OP_ADD <r:MultExpr> => Expr::add(l, r),
    <l:AddExpr> OP_SUB <r:MultExpr> => Expr::sub(l, r),
    MultExpr
}

MultExpr: Expr = {
    <l:MultExpr> OP_MULT <r:Term> => Expr::mult(l, r),
    <l:MultExpr> OP_DIV <r:Term> => Expr::div(l, r),
    Term
}

Term: Expr = {
    Ref,
    IntValue => Expr::Value(<>),
    FloatValue => Expr::Value(<>),
    DecimalValue => Expr::Value(<>),
    BoolValue => Expr::Value(<>),
    NoneValue => Expr::Value(<>),
    LBR <Expr> RBR
};

Ref: Expr = <s:IDENT> => Expr::reff(<>);

IntValue: Value = <s:INT> => Value::Int(i128::from_str(&s[1..]).unwrap());
FloatValue: Value = <s:FLOAT> => Value::Float(f64::from_str(&s[1..]).unwrap());
DecimalValue: Value = <s:DECIMAL> => Value::Decimal(Decimal::from_str(&s[1..]).unwrap());
BoolValue: Value = {
    TRUE => Value::Bool(true),
    FALSE => Value::Bool(false)
};
NoneValue: Value = NONE => Value::None;